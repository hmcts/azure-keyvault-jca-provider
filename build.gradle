plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'checkstyle'
  id 'org.owasp.dependencycheck' version '5.1.1'
  id 'com.jfrog.bintray' version '1.8.4'
}

group = 'uk.gov.hmcts.reform.security.keyvault'
version = '1.2.5'
sourceCompatibility = 1.8

dependencies {

  implementation 'org.projectlombok:lombok:1.18.2'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.2'

  implementation 'com.google.guava:guava:24.1-jre'
  implementation 'com.microsoft.azure:azure-keyvault:1.2.2'
  implementation 'com.microsoft.azure:adal4j:1.5.0'

  implementation 'org.apache.httpcomponents:httpclient:4.5.6'
  implementation 'org.apache.commons:commons-lang3:3.9'

  // Azure-KeyVault dependency uses an older version of databind which
  // has been flagged by the dependency-checker plugin
  implementation('com.fasterxml.jackson.core:jackson-databind:2.9.10.1') {
    force = true
  }

  // Use JUnit test framework
  testImplementation 'org.mockito:mockito-core:2.17.0'
  testImplementation "com.github.tomakehurst:wiremock:2.19.0"
  testImplementation 'junit:junit:4.12'
}

checkstyle {
  maxWarnings = 0
  toolVersion = 8.20
}

jacocoTestReport {
  reports {
    xml.enabled true
    csv.enabled false
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

check.dependsOn dependencyCheckAnalyze
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 0
  suppressionFile = 'config/dependency-check/dependency-check-suppressions.xml'
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      afterEvaluate {
        artifact sourcesJar
        artifact shadowJar
        groupId project.group
        artifactId rootProject.name
      }
      version project.version
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['Main']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = "${rootProject.name}"
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = "https://github.com/hmcts/${rootProject.name}"
    version {
      name = project.version
    }
  }
}

repositories {
  jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
